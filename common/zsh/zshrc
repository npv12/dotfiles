export ZSH_DIR="${ZDOTDIR}/.config/zsh"

# Initialize benchmark module if profiling enabled
if [[ "${ZSH_BENCHMARK:-}" == "1" ]]; then
    source "$ZSH_DIR/modules/benchmark/functions.zsh"
    _benchmark_init
fi

# Periodic auto-update on Zsh startup: 'ask' or 'no'.
# You can manually run `z4h update` to update everything.
zstyle ':z4h:' auto-update      'no'
# Ask whether to auto-update this often; has no effect if auto-update is 'no'.
zstyle ':z4h:' auto-update-days '28'

# Keyboard type: 'mac' or 'pc'.
zstyle ':z4h:bindkey' keyboard  'pc'

# Use brew installed fzf
zstyle ':z4h:*' fzf-command fzf

# Linux keybindings
zstyle ':z4h:autosuggestions' forward-char 'accept'

# Enable ('yes') or disable ('no') automatic teleportation of z4h over
# SSH when connecting to these hosts.
# zstyle ':z4h:ssh:*.example-hostname2' enable 'no'
# The default value if none of the overrides above match the hostname.
zstyle ':z4h:ssh:*'                   enable 'yes'

# Whether to move prompt to the bottom when zsh starts on Ctrl+L
zstyle ':z4h:' prompt-at-bottom 'yes'

# Mark up shell's output with semantic information.
zstyle ':z4h:' term-shell-integration 'yes'

# Recursively traverse directories when TAB-completing files.
zstyle ':z4h:fzf-complete' recurse-dirs 'yes'

# Enable direnv to automatically source .envrc files.
zstyle ':z4h:direnv'         enable 'no'

# Show "loading" and "unloading" notifications from direnv.
zstyle ':z4h:direnv:success' notify 'no'

# Send these files over to the remote host when connecting over SSH to the
# enabled hosts.
zstyle ':z4h:ssh:*' send-extra-files '~/.config/zsh'

# FZF
zstyle ':z4h:*' fzf-flags --color=hl:1,hl+:2

# Clone additional Git repositories from GitHub.
z4h install romkatv/zsh-defer hlissner/zsh-autopair bigH/git-fuzzy MichaelAquilina/zsh-you-should-use wfxr/forgit npv12/zsh-ai-cmd || return

function zsh_add_file() {
    local filename=$(echo "$1" | sed 's/\(.*\)\..*/\1/') # Strip out any extension
    [ -f "$ZSH_DIR/$filename.zsh" ] && z4h source "$ZSH_DIR/$filename.zsh"
}

typeset -ga __ZSH_DEFER_QUEUE=()
typeset -gi __ZSH_DEFER_RAN=0

function zsh_defer_add() {
    [[ -n "$1" ]] || return 0

    if [[ "${ZSH_BENCHMARK:-}" != "1" ]] && (( $+functions[zsh-defer] )); then
        zsh-defer eval "$1"
        return 0
    fi

    __ZSH_DEFER_QUEUE+=("$1")
}

function _zsh_run_deferred_queue() {
    emulate -L zsh
    (( __ZSH_DEFER_RAN )) && return 0
    __ZSH_DEFER_RAN=1

    local cmd
    local start_time
    local end_time
    local elapsed_ms

    while (( ${#__ZSH_DEFER_QUEUE[@]} > 0 )); do
        cmd="${__ZSH_DEFER_QUEUE[1]}"

        if (( ${#__ZSH_DEFER_QUEUE[@]} > 1 )); then
            __ZSH_DEFER_QUEUE=("${__ZSH_DEFER_QUEUE[@]:1}")
        else
            __ZSH_DEFER_QUEUE=()
        fi

        if [[ "${ZSH_BENCHMARK:-}" == "1" ]]; then
            start_time=$EPOCHREALTIME
        fi

        eval "$cmd"

        if [[ "${ZSH_BENCHMARK:-}" == "1" ]]; then
            end_time=$EPOCHREALTIME
            elapsed_ms=$(( (end_time - start_time) * 1000 ))
            (( $+functions[_benchmark_add_defer_time] )) && _benchmark_add_defer_time "$elapsed_ms"
        fi
    done

    add-zsh-hook -d precmd _zsh_run_deferred_queue 2>/dev/null
}

autoload -Uz add-zsh-hook
add-zsh-hook precmd _zsh_run_deferred_queue

zsh_add_file "zsh-binding"

# Install or update core components (fzf, zsh-autosuggestions, etc.) and
# initialize Zsh. After this point console I/O is unavailable until Zsh
# is fully initialized. Everything that requires user interaction or can
# perform network I/O must be done above. Everything else is best done below.
z4h init || return

# Additional repos
z4h load romkatv/zsh-defer

# Define key bindings.
z4h bindkey z4h-backward-kill-word  Ctrl+Backspace     Ctrl+H
z4h bindkey z4h-cd-back    Shift+Left   # cd into the previous directory
z4h bindkey z4h-cd-forward Shift+Right  # cd into the next directory
z4h bindkey z4h-cd-up      Shift+Up     # cd into the parent directory
z4h bindkey z4h-cd-down    Shift+Down   # cd into a child directory

# Autoload functions.
autoload -Uz zmv

# Set shell options: http://zsh.sourceforge.net/Doc/Release/Options.html.
setopt glob_dots     # no special treatment for file names with a leading dot
setopt no_auto_menu  # require an extra TAB press to open the completion menu
setopt correct_all

z4h source "$ZSH_DIR/p10k.zsh"

# Load the modules defer
zsh_defer_add 'z4h load wfxr/forgit npv12/zsh-ai-cmd hlissner/zsh-autopair bigH/git-fuzzy'
zsh_defer_add 'z4h source $Z4H/MichaelAquilina/zsh-you-should-use/you-should-use.plugin.zsh' # Manually load it instead since z4h doesn't support it
zsh_defer_add 'zsh_add_file "controller"'

# Display benchmark results if profiling enabled
if [[ "${ZSH_BENCHMARK:-}" == "1" ]]; then
    _benchmark_set_ready_time $(( ($EPOCHREALTIME - ZSH_BENCHMARK_START) * 1000 ))
    _zsh_run_deferred_queue
    _benchmark_report
fi
